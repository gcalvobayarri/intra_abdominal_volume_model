model{

  for(i in 1 : N){
   for( j in 1 : individual_sizes[i]){
     Y[i, j] ~ dnorm(Mi[i, j], lambda) 
     Mi[i, j] <- a[i] / (1 + exp(-c[i] * X[i, j]-b[i]))
   }
  }


  ## PRIORS
  # sigma prior
  
  lambda <- 1 / (sigma * sigma)
  sigma ~ dunif(0,10)
  
  # a, b, c prior
  for(i in 1 : N){
    a[i] <- beta0[1] + u_a[i] + betaW[1] * sex[i] +  betaA[1] * age[i] 
    b[i] <- beta0[2] + u_b[i] + betaW[2] * sex[i] +  betaA[2] * age[i]
    c[i] <- beta0[3] +  betaW[3] * sex[i] 


    # PDA

    PDA[i] <- -(log(5-2*sqrt(6)) + b[i]) / c[i]
 }

  # beta prior

  for(i in 1 : 2){
    betaA[i] ~ dnorm(0, 0.01)
    
  }

  for(i in 1 : 3){
    betaW[i] ~ dnorm(0, 0.01)
  }

  beta0[1] ~ dunif(0, 20)
  beta0[2] ~ dnorm(0, 0.01)
  beta0[3] ~ dunif(0, 10)

  for(i in 1 : N){
    u_a[i] ~ dnorm(0, Alambda)
    u_b[i] ~ dnorm(0, Blambda)
  }


  # lambda_a,b,c prior
  Alambda <- 1 / (sigmaA * sigmaA)
  sigmaA ~ dunif(0,10)

  Blambda <- 1 / (sigmaB * sigmaB)
  sigmaB ~ dunif(0,10)

}